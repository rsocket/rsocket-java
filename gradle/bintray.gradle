/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey') &&
        project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {

    subprojects {
        plugins.withId('com.jfrog.bintray') {
            bintray {
                user = project.property('bintrayUser')
                key = project.property('bintrayKey')

                publications = ['maven']
                publish = true
                override = true

                pkg {
                    repo = 'RSocket'
                    name = 'rsocket-java'
                    licenses = ['Apache-2.0']

                    issueTrackerUrl = 'https://github.com/rsocket/rsocket-java/issues'
                    websiteUrl = 'https://github.com/rsocket/rsocket-java'
                    vcsUrl = 'https://github.com/rsocket/rsocket-java.git'

                    githubRepo = 'rsocket/rsocket-java' //Optional Github repository
                    githubReleaseNotesFile = 'README.md' //Optional Github readme file

                    version {
                        name = project.version
                        released = new Date()
                        vcsTag = project.version

                        gpg {
                            sign = true
                        }

                        mavenCentralSync {
                            user = project.property('sonatypeUsername')
                            password = project.property('sonatypePassword')
                        }
                    }
                }
            }
            tasks.named("bintrayUpload").configure {
                onlyIf { System.getenv('SKIP_RELEASE') != "true" }
            }
        }
    }
}
